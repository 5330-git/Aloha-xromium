# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//aloha/aloha_paks.gni")
import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")

static_library("aloha_webview_lib") {
  testonly = true
  sources = [
    "app/mac_init.h",
    "app/mac_init.mm",
    "app/main_delegate.cc",
    "app/main_delegate.h",
    "browser/browser_context.cc",
    "browser/browser_context.h",
    "browser/browser_main_parts.cc",
    "browser/browser_main_parts.h",
    "browser/content_browser_client.cc",
    "browser/content_browser_client.h",
    "browser/ui/web/browser.cc",
    "browser/ui/web/browser.h",
    "browser/ui/web/browser_page_handler.cc",
    "browser/ui/web/browser_page_handler.h",
    "browser/ui/web/guest_view_manager_delegate.cc",
    "browser/ui/web/guest_view_manager_delegate.h",
    "browser/ui/web/web_view.cc",
    "browser/ui/web/web_view.h",
    "browser/ui/web/webshell_guest_view.cc",
    "browser/ui/web/webshell_guest_view.h",
    "browser/ui/web/webui.cc",
    "browser/ui/web/webui.h",
    "browser/webui_controller_factory.cc",
    "browser/webui_controller_factory.h",
    "common/content_client.cc",
    "common/content_client.h",
    "renderer/content_renderer_client.cc",
    "renderer/content_renderer_client.h",
    "renderer/render_frame_observer.cc",
    "renderer/render_frame_observer.h",
  ]

  public_deps = [
    "//aloha/browser/client:aloha_content_client",
    "//aloha/browser/devtools:aloha_devtools",
    "//aloha/browser/ui/views:views",
    "//aloha/common:common",
  ]

  deps = [
    ":browser_mojo_bindings",
    ":resources",
    "resources/browser:resources",
    "resources/vector_icons:vector_icons",
    "//aloha/resources:app_icon_resources",
    "//aloha/resources:unscaled_resources",
  ]

  data_deps = [ "//tools/v8_context_snapshot" ]

  if (use_aura) {
    sources += [
      "browser/browser_main_parts_aura.cc",
      "browser/ui/aura/aura_context.cc",
      "browser/ui/aura/aura_context.h",
      "browser/ui/aura/content_window.cc",
      "browser/ui/aura/content_window.h",
      "browser/ui/aura/fill_layout.cc",
      "browser/ui/aura/fill_layout.h",
    ]
    deps += [
      "//ui/aura:aura",
      "//ui/aura:test_support",
      "//ui/base",
      "//ui/base/ime/init",
      "//ui/compositor",
      "//ui/compositor:test_support",
      "//ui/display",
      "//ui/events",
    ]
  }

  if (is_mac) {
    sources += [
      "browser/browser_main_parts_mac.mm",
      "browser/ui/cocoa/content_ns_window.h",
      "browser/ui/cocoa/content_ns_window.mm",
    ]
    frameworks = [
      "AppKit.framework",
      "Cocoa.framework",
    ]
  }
}

# 新增的资源应该在 aloha_paks.gni 中添加
aloha_paks("aloha_all_resources") {
  output_dir = root_out_dir
}

group("resources") {
  deps = [ ":aloha_all_resources" ]
}

mojom("browser_mojo_bindings") {
  sources = [ "browser/ui/web/browser.mojom" ]
  public_deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
  webui_module_path = "/"
}

executable("aloha") {
  sources = [
    "app/aloha_content_main_delegate.cc",
    "app/aloha_content_main_delegate.h",
    "app/aloha_main.cc",
    "app/aloha_main.h",
  ]
  deps = [
    ":resources",
    "//aloha/browser:browser",
  ]
  if (is_win) {
    configs += [ "//build/config/win:windowed" ]

    deps += [
      "//build/win:default_exe_manifest",
      "//content:sandbox_helper_win",
      "//sandbox/win:sandbox",
    ]
    sources += [
      "app/aloha_exe_main_win.cc",
      "resources/aloha_exe.rc",
    ]
  }

  if (is_mac) {
    deps += [ "//sandbox/mac:seatbelt" ]
  }
}

executable("aloha_webui_exe") {
  testonly = true

  sources = [
    "app/aloha_content_main_delegate.cc",
    "app/aloha_content_main_delegate.h",
    "app/aloha_main.cc",
    "app/aloha_main.h",
    "app/main.cc",
  ]

  deps = [
    ":aloha_webview_lib",
    "//aloha/browser/client:aloha_content_client",
    "//aloha/common:common",
    "//base",
    "//content/public/app",
    "//ui/aura:aura",
    "//ui/views:views",
  ]

  if (is_win) {
    configs += [ "//build/config/win:windowed" ]

    deps += [
      "//build/win:default_exe_manifest",
      "//content:sandbox_helper_win",
      "//sandbox/win:sandbox",
    ]
    sources += [ "resources/aloha_exe.rc" ]
  }

  if (is_mac) {
    deps += [ "//sandbox/mac:seatbelt" ]
  }
}

group("aloha-all") {
  deps = [
    ":aloha",
    "//aloha/resources/browser:build_webapp_resources",
    "//aloha/installer:aloha_installer",
  ]
}
