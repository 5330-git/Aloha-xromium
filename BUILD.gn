# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")

static_library("aloha_webview_lib") {
  testonly = true

  sources = [
    "app/mac_init.h",
    "app/mac_init.mm",
    "app/main_delegate.cc",
    "app/main_delegate.h",
    "browser/browser_context.cc",
    "browser/browser_context.h",
    "browser/browser_main_parts.cc",
    "browser/browser_main_parts.h",
    "browser/content_browser_client.cc",
    "browser/content_browser_client.h",
    "browser/ui/web/browser.cc",
    "browser/ui/web/browser.h",
    "browser/ui/web/browser_page_handler.cc",
    "browser/ui/web/browser_page_handler.h",
    "browser/ui/web/guest_view_manager_delegate.cc",
    "browser/ui/web/guest_view_manager_delegate.h",
    "browser/ui/web/web_view.cc",
    "browser/ui/web/web_view.h",
    "browser/ui/web/webshell_guest_view.cc",
    "browser/ui/web/webshell_guest_view.h",
    "browser/ui/web/webui.cc",
    "browser/ui/web/webui.h",
    "browser/webui_controller_factory.cc",
    "browser/webui_controller_factory.h",
    "common/content_client.cc",
    "common/content_client.h",
    "renderer/content_renderer_client.cc",
    "renderer/content_renderer_client.h",
    "renderer/render_frame_observer.cc",
    "renderer/render_frame_observer.h",
  ]

  public_deps = [
    "//aloha/browser/devtools:aloha_devtools",
    "//aloha/browser/ui/views:views",
    "//aloha/views_content_client",
    "//aloha/views_content_client:views_content_client",
  ]

  deps = [
    ":browser_mojo_bindings",
    ":pak",
    ":resources_grit",
    "resources/browser:resources",
    "resources/vector_icons:vector_icons",
    "//base",
    "//chrome/browser/resources/webui_gallery:resources",
    "//components/guest_view/browser",
    "//components/guest_view/common:mojom",
    "//components/guest_view/renderer",
    "//components/input:input",
    "//content/public/app",
    "//content/public/browser",
    "//content/public/renderer",
    "//ipc",
    "//mojo/public/cpp/bindings",
    "//net",
    "//third_party/blink/public:blink",
    "//ui/base",
    "//ui/display",
    "//ui/platform_window",
    "//ui/views",
    "//ui/views/controls/webview:webview",
    "//ui/webui",
    "//ui/wm:wm",
    "//v8:v8_headers",
  ]

  data_deps = [ "//tools/v8_context_snapshot" ]

  if (use_aura) {
    sources += [
      "browser/browser_main_parts_aura.cc",
      "browser/ui/aura/aura_context.cc",
      "browser/ui/aura/aura_context.h",
      "browser/ui/aura/content_window.cc",
      "browser/ui/aura/content_window.h",
      "browser/ui/aura/fill_layout.cc",
      "browser/ui/aura/fill_layout.h",
    ]
    deps += [
      "//ui/aura:aura",
      "//ui/aura:test_support",
      "//ui/base",
      "//ui/base/ime/init",
      "//ui/compositor",
      "//ui/compositor:test_support",
      "//ui/display",
      "//ui/events",
    ]
  }

  if (is_mac) {
    sources += [
      "browser/browser_main_parts_mac.mm",
      "browser/ui/cocoa/content_ns_window.h",
      "browser/ui/cocoa/content_ns_window.mm",
    ]
    frameworks = [
      "AppKit.framework",
      "Cocoa.framework",
    ]
  }
}

grit("resources") {
  testonly = true
  source = "resources/aloha_resources.grd"
  outputs = [
    "grit/aloha_resources.h",
    "aloha_resources.pak",
  ]
}

repack("pak") {
  testonly = true
  sources = [
    "$root_gen_dir/aloha/resources/browser/aloha_browser_resources.pak",

    # "$root_gen_dir/chrome/webui_gallery_resources.pak",
    "$root_gen_dir/content/browser/devtools/devtools_resources.pak",

    # fix CSSDefaultStyleSheet loss default_media_control_style（$root_gen_dir/third_party/blink/renderer/modules/media_controls/resources/media_controls_resources_100_percent.pak）
    "$root_gen_dir/third_party/blink/public/resources/blink_scaled_resources_100_percent.pak",

    # blink 资源提前补充：
    "$root_gen_dir/third_party/blink/public/resources/inspector_overlay_resources.pak",

    # fix blink_strings loss
    "$root_gen_dir/third_party/blink/public/strings/blink_strings_af.pak",

    # aloha 自定义资源
    "$target_gen_dir/aloha_resources.pak",

    # webui_examples 和 views_examples_with_content 重复的
    # "$root_gen_dir/mojo/public/js/mojo_bindings_resources.pak",
    # "$root_gen_dir/third_party/blink/public/resources/blink_resources.pak",
    # "$root_gen_dir/ui/resources/webui_resources.pak",
    # "$root_gen_dir/ui/strings/app_locale_settings_en-US.pak",
    # "$root_gen_dir/ui/strings/ui_strings_en-US.pak",

    # 这里应该在 PreSandboxStartup 中加载，而不是静态打包：
    # ~把 ui/resources 的资源打包进来, 否则 52613 资源找不到会触发 ImageSkia 的CHECK~
    # "$root_gen_dir/ui/resources/ui_resources_100_percent.pak",
  ]
  deps = [
    ":resources",
    "resources/browser:resources",
    "//chrome/browser/resources/webui_gallery:resources",
    "//content/browser/devtools:devtools_resources",
    "//mojo/public/js:resources",
    "//third_party/blink/public:devtools_inspector_resources",
    "//third_party/blink/public:image_resources",
    "//third_party/blink/public:resources",
    "//third_party/blink/public:scaled_resources_100_percent",
    "//third_party/blink/public/strings:strings",
    "//ui/resources",
    "//ui/strings",
  ]
  output = "$root_out_dir/aloha.pak"
}

mojom("browser_mojo_bindings") {
  sources = [ "browser/ui/web/browser.mojom" ]
  public_deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
  webui_module_path = "/"
}

executable("aloha") {
  testonly = true

  sources = [ "app/main.cc" ]

  deps = [
    ":aloha_webview_lib",
    "//aloha/views_content_client:views_content_client",
    "//base",
    "//content/public/app",
    "//ui/aura:aura",
    "//ui/views:views",
  ]

  if (is_win) {
    configs += [ "//build/config/win:windowed" ]

    deps += [
      "//build/win:default_exe_manifest",
      "//content:sandbox_helper_win",
      "//sandbox/win:sandbox",
    ]
  }

  if (is_mac) {
    deps += [ "//sandbox/mac:seatbelt" ]
  }
}
