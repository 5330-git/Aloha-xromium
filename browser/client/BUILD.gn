# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")

source_set("aloha_content_client") {
  testonly = true
  sources = [
    "aloha_content_browser_client.cc",
    "aloha_content_browser_client.h",
    "aloha_content_client_main_parts.cc",
    "aloha_content_client_main_parts.h",
    "aloha_content_main_delegate.cc",
    "aloha_content_main_delegate.h",
  ]

  if (is_chromeos_ash) {
    sources += [ "aloha_content_client_main_parts_chromeos.cc" ]
  }

  if (is_mac) {
    sources += [ "aloha_content_client_main_parts_mac.mm" ]
  }

  deps = [
    "//aloha/browser/devtools:aloha_devtools",
    "//aloha/browser/profile:profile",
    "//aloha/browser/ui/views:views",
    "//aloha/common:common",
    "//base",
    "//base:i18n",
    "//build:chromeos_buildflags",
    "//components/renderer_context_menu:renderer_context_menu",
    "//content",
    "//content/shell:content_shell_lib",
    "//third_party/icu",
    "//ui/base",
    "//ui/base/ime/init",
    "//ui/display",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/resources",
    "//ui/resources:ui_test_pak",
    "//ui/resources:ui_test_pak_data_200_percent",
    "//ui/views:test_support",
    "//ui/views:views",
    "//ui/views/controls/webview:webview",
  ]

  if (use_aura) {
    sources += [
      "aloha_content_client_main_parts_aura.cc",
      "aloha_content_client_main_parts_aura.h",
    ]

    if (!is_chromeos_ash) {
      sources += [ "aloha_content_client_main_parts_desktop_aura.cc" ]
    }

    deps += [
      "//ui/aura",
      "//ui/wm",
    ]
  }

  if (is_chromeos_ash) {
    deps += [
      "//ui/aura:test_support",
      "//ui/wm:test_support",
    ]
  }
}
