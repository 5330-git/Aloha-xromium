# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")

source_set("aloha_content_client") {
  sources = [
    "aloha_content_browser_client.cc",
    "aloha_content_browser_client.h",
    "aloha_content_client_main_parts.cc",
    "aloha_content_client_main_parts.h",
  ]

  if (is_chromeos_ash) {
    sources += [ "aloha_content_client_main_parts_chromeos.cc" ]
  }

  if (is_mac) {
    sources += [ "aloha_content_client_main_parts_mac.mm" ]
  }

  deps = [
    "//aloha:resources",
    "//aloha/browser/devtools:aloha_devtools",
    "//aloha/browser/profile:profile",
    "//aloha/browser/ui/views:views",
    "//aloha/browser/url:url",
    "//aloha/common:common",
    "//components/prefs:prefs",
    "//content:content",
    "//services/cert_verifier/public/mojom:mojom",
    "//services/network/public/cpp:cpp",
    "//services/network/public/cpp:cpp_base",
    "//services/network/public/mojom:mojom",
    "//ui/base:base",
    "//ui/views:views",
  ]

  if (use_aura) {
    sources += [
      "aloha_content_client_main_parts_aura.cc",
      "aloha_content_client_main_parts_aura.h",
    ]

    if (!is_chromeos_ash) {
      sources += [ "aloha_content_client_main_parts_desktop_aura.cc" ]
    }

    deps += [
      "//ui/aura",
      "//ui/wm",
    ]
  }

  if (is_chromeos_ash) {
    deps += [
      "//ui/aura:test_support",
      "//ui/wm:test_support",
    ]
  }
}
